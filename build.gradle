plugins {
	id 'base'
	id 'java'
	id 'maven-publish'
	id "dev.architectury.loom" version "1.6-SNAPSHOT"
	id "com.matthewprenger.cursegradle" version "1.4.0"
	id "com.modrinth.minotaur" version "2.2.0"
}

ext.ENV = System.getenv()

repositories {
	maven {
		url "https://maven.neoforged.net/releases"
	}
	maven {
		url "https://maven.architectury.dev/"
	}
	maven {
		name = "FTB Library"
		url = "https://maven.saps.dev/minecraft"
	}
}

version = mod_version
group = project.maven_group
base.archivesBaseName = project.archives_base_name

loom {
	silentMojangMappingsLicense()

	runs {
		client {
			vmArgs "-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition", "-XX:HotswapAgent=fatjar"
		}
		server {
			vmArgs "-XX:+IgnoreUnrecognizedVMOptions", "-XX:+AllowEnhancedClassRedefinition", "-XX:HotswapAgent=fatjar"
		}
	}

	accessWidenerPath = file("src/main/resources/gag.accesswidener")
}

dependencies {
	minecraft "com.mojang:minecraft:${minecraft_version}"
	mappings loom.officialMojangMappings()

	neoForge("net.neoforged:neoforge:${neoforge_version}")

	modApi "dev.architectury:architectury-neoforge:${architectury_version}"
	include(implementation("dev.ftb.mods:ftb-library-neoforge:${config_version}"))

	modLocalRuntime modCompileOnly("me.shedaniel:RoughlyEnoughItems-neoforge:${rei_version}")
}

processResources {
	def toReplace = [
			"version"             : project.version,
			"architectury_version": architectury_version
	]

	inputs.properties toReplace
	filesMatching("META-INF/mods.toml") {
		expand toReplace
	}
}

compileJava {
	options.encoding = "UTF-8"
	options.release.set(17)
}

java {
	sourceCompatibility = targetCompatibility = '17'
	withSourcesJar()
}

if (ENV.CURSE_API_KEY) {
	curseforge {
		apiKey = ENV.CURSE_API_KEY
		project {
			id = rootProject.curseforge_id
			changelog = getGitChangelog
			changelogType = 'markdown'
			releaseType = rootProject.artifact_type

			addGameVersion '1.20.4'
			addGameVersion 'NeoForge'

			relations {
				requiredDependency 'architectury-api'
				requiredDependency 'ftb-library-forge'
			}

			mainArtifact(remapJar) {
				displayName = "[NeoForge] $mod_name $project.version"
			}
		}
	}
}


publishing {
	publications {
		mavenForge(MavenPublication) {
			artifactId = "${rootProject.archives_base_name}-${project.name}"
			from components.java
		}
	}

	repositories {
		if (ENV.SAPS_TOKEN) {
			maven {
				url "https://maven.saps.dev/minecraft"
				credentials {
					username = "max"
					password = "${ENV.SAPS_TOKEN}"
				}
			}
		}
	}
}
